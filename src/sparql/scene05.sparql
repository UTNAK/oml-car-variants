PREFIX ad: <http://schemas.talis.com/2005/address/schema#>
PREFIX analysis:  <http://imce.jpl.nasa.gov/foundation/analysis#>
PREFIX constraint: <http://opencaesar.io/firesat-telecom/telecom-supplier/vocabulary/constraint#>
PREFIX rdfs:      <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ads:       <http://opencaesar.io/template/vocabulary/ADSdomain#>
PREFIX ec:        <http://opencaesar.io/template/vocabulary/EnverionmentalCondition/EnverionmentalCondition#>

SELECT DISTINCT ?iri ?weatherPattern ?SVVehicleType ?SVposx ?SVposy ?SVposz ?SVvel ?SVacc ?SVsteering ?SVorientation
                ?PovVehicleType ?Povposx ?Povposy ?Povposz ?Povvel ?Povacc ?Povsteering ?Povorientation
WHERE {
  VALUES ?iri { <http://opencaesar.io/template/description/scene/scene05#ads-test-case01> }

  ?iri a ads:AutomatedDrivingSystemDomain ;
       ads:hasJASPARODD [ ads:hasEC [ ec:weatherPattern ?weatherPattern ] ;
                          ads:hasTP ?pov ] ;
       ads:hasAV [ ads:hasVehicleType [ ads:hasString ?SVVehicleType ] ;
                   ads:hasPos-X      [ ads:hasFloatNumber ?SVposx ] ;
                   ads:hasPos-Y      [ ads:hasFloatNumber ?SVposy ] ;
                   ads:hasPos-Z      [ ads:hasFloatNumber ?SVposz ] ;
                   ads:hasVelocity   [ ads:hasFloatNumber ?SVvel ] ;
                   ads:hasAcceleration [ ads:hasFloatNumber ?SVacc ] ;
                   ads:hasSteering   [ ads:hasFloatNumber ?SVsteering ] ;
                   ads:hasOrientation[ ads:hasFloatNumber ?SVorientation ] ] .

  OPTIONAL {
    ?pov ads:hasVehicleType [ ads:hasString ?PovVehicleType ] ;
         ads:hasPos-X       [ ads:hasFloatNumber ?Povposx ] ;
         ads:hasPos-Y       [ ads:hasFloatNumber ?Povposy ] ;
         ads:hasPos-Z       [ ads:hasFloatNumber ?Povposz ] ;
         ads:hasVelocity    [ ads:hasFloatNumber ?Povvel ] ;
         ads:hasAcceleration[ ads:hasFloatNumber ?Povacc ] ;
         ads:hasSteering    [ ads:hasFloatNumber ?Povsteering ] ;
         ads:hasOrientation [ ads:hasFloatNumber ?Povorientation ] .
  }
}